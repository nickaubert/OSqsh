#!/usr/bin/perl

##########################################################################
# Oracle client CLI wrapper for SQLPlus that adds Readline support.
#
# This isn't likely to be any kind of full replacement for SQLPlus,
# but readline support should make it more user friendly (if a modern
# CLI is your friend).
#
##########################################################################

use warnings;
use strict;
use Term::ReadLine;
use DBD::Oracle;
use Getopt::Long;


## Command Line Interface options
#
my $cli_opts_ref = &loadcliopts;
my %cli_opts = %$cli_opts_ref;
my $debug = $cli_opts{'debug'};
if ( $debug ) { print "debug on\n"; }


## load contents of rc file if any
#
my $rcf_opts_ref = &load_rcfile;
my %rcf_opts = %$rcf_opts_ref;


## assign mandatory values
#  make sure each of these values has something in it
#  load_neededvars subroutine modifies %ENV
#
my @need_oravals = qw( 
    ORACLE_HOME
    ORACLE_SID
    ORACLE_USER
    ORACLE_PASS
    TNS_ADMIN
);
my $oracle_settings_ref = &load_oracle_vals ( \%cli_opts , \%rcf_opts , @need_oravals );
my %oracle_settings = %$oracle_settings_ref;


## connect to database
#
my $dbh = DBI->connect(
    "dbi:Oracle:$oracle_settings{'ORACLE_SID'}" ,
    "$oracle_settings{'ORACLE_USER'}" ,
    "$oracle_settings{'ORACLE_PASS'}" , 
    {
        RaiseError => 0,
        AutoCommit => 1,
        ora_check_sql => 0,
        RowCacheSize => 16
    }
) || die( $DBI::errstr . "\n" );


## set up readline with tab completion and load miscellaneous tab-completion words
#
my $term = Term::ReadLine->new('OSqsh');
my @user_tables = &load_usertables( $dbh );
my %nice_tabs = (
    basic_cmd => [ 'select' , 'insert' , 'into' , 'create' , 'from' , 'where' , 
                    'group by' , 'having' , 'exists' , 'between' , 'min' , 'max ' ],
    data_type => [ 'varchar2' , 'number' , 'timestamp' ],
    sys_table => [ 'dual' ],
    sqlplscmd => [ 'describe' , 'exit' , 'quit' ],
    usr_table => \@user_tables,
    usr_colmn => [ ]
);
&load_completion( $term , \%nice_tabs );


## now run the prompt
#
my @multiline_cmd;
my $prompt = "OSQSH> ";
while ( defined ( my $command = $term->readline($prompt)) ) {

    # rl_reset_after_signal();
    # if ( $command =~ /TRAP-SIGINT/) { print "\n" unless $term->ReadLine eq 'Term::ReadLine::Perl'; # clear screen } 

    ## quit (^D also works)
    #
    if ( $command =~ /^(quit|exit)\W*/i ) {
        exit 0;

    ## should be ctrl-c
    #
    } elsif ( $command =~ /^clear$/i ) {
        @multiline_cmd = ();


    ## reproduce DESCRIBE sqlplus command
    #  add table column headers for tab completion
    #
    } elsif ( $command =~ /^des(c|cr|cri|crib|cribe)\s+(.*)$/i ) {

        my @user_tables = &load_usertables( $dbh );
        my @arguments = split /\s+/ , $2;
        foreach my $this_argument ( @arguments ) {
            last if ( $this_argument eq ';' );
            my ( $display_desc , @desc_return ) = &desc_table( $dbh , $this_argument );
            push @{$nice_tabs{'usr_colmn'}} , @desc_return;
            &load_completion( $term , \%nice_tabs);
            if ( @arguments > 1 ) {
                print "\n$this_argument:";
            }
            print "$display_desc";
        }

    ## any line that contains a semicolon will be executed (anything after the semicolon is ignored)
    #
    } elsif ( $command =~ /;/ ) {

        ## strip semicolon and anything after
        #
        $command =~ s/;.*$//;

        my $full_cmd;
        if ( @multiline_cmd > 0 ) {
            push @multiline_cmd , $command;
            $full_cmd = join ' '  , @multiline_cmd;
            $term->addhistory("$full_cmd;");
            @multiline_cmd = ();
        } else {
            $full_cmd = $command;
        }

        my ( $result_ref , $result_columninfo_ref ) = &run_sql_command( $dbh , $full_cmd );
        if ( ( ref($result_ref) eq 'ARRAY' ) and ( ref($result_columninfo_ref) eq 'ARRAY' ) ) {
            my $result_out = &format_result( $result_ref , $result_columninfo_ref );
            print "$result_out";
        } else {
            if ( $debug ) { print "\$result_ref: $result_ref\n"; }
        }

    ## anything else is considered a multiline command
    #
    } else {
        push @multiline_cmd , $command;
    }

    ## show the buffer if anything is in it
    #
    if ( @multiline_cmd > 0 ) {
        my $display_multiline_cmd = join "\n       " , @multiline_cmd;
        print "       $display_multiline_cmd\n";
    }

}

print "\n";
END {
    $dbh->disconnect if defined($dbh);
}


##########################################################################
# Subroutines
##########################################################################

##########################################################################
# sub load_rcfile
#   input: none
#   return: reference to hash containing settings in rc file
#       (hash will be empty if there there is no rc file)
#   Reads values from ~/.osqshrc
##########################################################################
sub load_rcfile {

    my %local_rc_vars;
    unless ( defined $ENV{'HOME'} ) {
        return \%local_rc_vars;
    }

    my $rc_file = "$ENV{'HOME'}/.osqshrc";
    unless ( -f $rc_file ) {
        return \%local_rc_vars;
    }

    unless ( open ( RCFILE , "$rc_file" ) ) {
        warn "Unable to open $rc_file\n";
        return \%local_rc_vars;
    }

    while (<RCFILE>) {
        chomp;              # no newline
        s/#.*//;            # no comments
        s/^\s+//;           # no leading white
        s/\s+$//;           # no trailing white
        next unless m/(.+)\s*=\s*(.+)/;
        if ( defined($1) and defined($2) ) {
            # my $key   = uc($1);
            # my $value    = $2;
            # $local_rc_vars{$key} = $value;
            $local_rc_vars{uc($1)} = $2;
        }
    }

    close (RCFILE);
    return \%local_rc_vars;

}


##########################################################################
# sub load_oracle_vals
#   input:
#       options hash ref,
#       rc file values hash ref,
#       list of mandatory variables
#   return: reference to hash containing oracle settings
#       also updates %ENV
#   Makes sure required Oracle variables are loaded
#   value prioritization is: 
#       1. command line option
#       2. environment variable
#       3. rc file
#       4. if all above are empty, then ask nicely
##########################################################################
sub load_oracle_vals {

    my ( $cli_opts_ref , $rcf_opts_ref , @need_oravals ) = @_;
    my %cli_opts = %$cli_opts_ref;
    my %rcf_opts = %$rcf_opts_ref;
    my %oracle_settings;

    ## first, load cli options into settings
    #
    foreach my $this_cli_opt ( keys %cli_opts ) {
        $oracle_settings{$this_cli_opt} = $cli_opts{$this_cli_opt};
    }

    ## second, load environment variables into settings
    #  if they haven't already been defined on the cli
    #
    foreach my $this_oravar ( @need_oravals ) {
        if (
          ( ! defined $oracle_settings{$this_oravar} ) and
          (   defined $ENV{$this_oravar}             ) ) {
            $oracle_settings{$this_oravar} = $ENV{$this_oravar};
        }
    }

    ## third, check rc file
    #
    foreach my $this_rcf_opt ( keys %rcf_opts ) {

        ## warn if environment overrides an rc setting 
        #  because this can drive you crazy
        #
        if (
          ( defined $ENV{$this_rcf_opt} ) and
          ( $ENV{$this_rcf_opt} ne $rcf_opts{$this_rcf_opt}) ) {
            warn "$this_rcf_opt in environment overrides rcfile.\n";
            next;
        }

        ## don't bother to warn for CLI overrides
        #
        next if ( defined $cli_opts{$this_rcf_opt} );
        $oracle_settings{$this_rcf_opt} = $rcf_opts{$this_rcf_opt};
    }

    ## fourth assign any mandatory values that haven't already been assigned
    #
    foreach my $this_oravar ( @need_oravals ) {

        if ( ! defined $oracle_settings{$this_oravar} ) {
            print "Enter $this_oravar: ";
            my $input_var = <STDIN>;
            chomp $input_var;
            $oracle_settings{$this_oravar} = $input_var;
        }

        ## make sure environment matches
        #
        $ENV{$this_oravar} = $oracle_settings{$this_oravar};

        if ( $debug ) { print "We have added $this_oravar is $oracle_settings{$this_oravar}\n"; }
    }

    return \%oracle_settings;
}


##########################################################################
# sub load_usertables
#   input: database handle
#   return: list of table names
#   Get the table names from USER_TABLES
##########################################################################
sub load_usertables {

    my ( $dbh_ref ) = @_;
    my $cmd = "SELECT TABLE_NAME FROM USER_TABLES";
    my @load_tables = map { $_ = $_->{'TABLE_NAME'}; } @{ $dbh->selectall_arrayref( $cmd, { Slice => {} }) };
    return @load_tables;

}


##########################################################################
# sub load_completion
#   input: ref to Term::ReadLine object , ref to hash with various words to complete
#   return: list of possible words to auto-complete
#   Readline tab completion with minimal intelligence
##########################################################################
sub load_completion {

    my ( $term_ref , $nice_tabs_ref ) = @_;

    ## just load everything into a big list
    #
    my %local_tabs = %$nice_tabs_ref;
    my @these_words;
    foreach my $this_key ( keys %$nice_tabs_ref ) {
        push @these_words , @{$local_tabs{$this_key}};
    }

    ## remove duplicates and add both upper and lower case
    #
    my %lc_keys;
    map { $lc_keys{lc($_)} = '' } @these_words;
    my @tab_words = ( keys %lc_keys , map { uc($_) } keys %lc_keys );

    ## hack
    #
    my @just_tables = @{$local_tabs{'usr_table'}};
    push @just_tables , map { lc($_) } @just_tables;

    ## hack
    #  if we dont know about table columns, return whatever we have
    #
    my @just_columns = @{$local_tabs{'usr_colmn'}};
    push @just_columns , map { lc($_) } @just_columns;
    if ( @just_columns == 0 ) { @just_columns = @tab_words; }

    $term_ref->Attribs->{attempted_completion_function} = sub {
        my ($text, $line, $start, $end) = @_;
        if ( $line =~ m/\s+FROM\s+\w*$/i ) {
            $term_ref->Attribs->{completion_word} = [@just_tables];
        } elsif ( $line =~ m/SELECT\s+\w*$/i ) {
            $term_ref->Attribs->{completion_word} = [@just_columns];
        } elsif ( $line =~ m/^des(c|cr|cri|crib|cribe)\s+/i ) {
            $term_ref->Attribs->{completion_word} = [@just_tables];
        } else {
            $term_ref->Attribs->{completion_word} = [@tab_words];
        }

        my $front_bound = '';
        if ( $text =~ s/^(\w+\.)// ) {
            $front_bound = $1;
        }
        my @ret = $term->completion_matches($text, $term_ref->Attribs->{list_completion_function});
        @ret = map { $front_bound.$_ } @ret;
        return @ret;
    };

    return 0;
}


##########################################################################
# sub get_column_names
#   input: database handle, table name
#   return: list of column names in given table
#   Get column names for a given table.  Used by DESCRIBE surrogate
##########################################################################
sub get_column_names {

    my ( $dbh_ref , $read_table ) = @_;
    my $cmd = uc("SELECT COLUMN_NAME FROM ALL_COL_COMMENTS WHERE TABLE_NAME = '$read_table'");
    my @load_columns = map { $_ = $_->{COLUMN_NAME}; } @{ $dbh->selectall_arrayref( $cmd, { Slice => {} })};
    return @load_columns;

}


##########################################################################
# sub get_column_data
#   input: database handle, prepared sql statement handle
#   return: reference to list of arrays containing information on table columns
#   Get column information for a given table used in sql statement.
#   It would be nice to return this info in a hash instead of an array,
#   but it works better if this structure mirrors the array containing result set.
##########################################################################
sub get_column_data {

    ## collect column data
    #  there MUST be a better way to do this
    #
    my ( $dbh_ref , $sth_ref ) = @_;
    my @type_names   = map { scalar $dbh_ref->type_info($_)->{TYPE_NAME} } @{ $sth_ref->{TYPE} };
    my @column_names = @{ $sth_ref->{NAME}      };
    my @precision    = @{ $sth_ref->{PRECISION} };
    my @scale        = @{ $sth_ref->{SCALE}     };
    my @column_stuff;
    my $count_columns = 0;
    foreach my $this_col_name ( @column_names ) {
        $column_stuff[$count_columns]{'col_name'}  = $this_col_name;
        $column_stuff[$count_columns]{'col_type'}  = $type_names[$count_columns];
        $column_stuff[$count_columns]{'precision'} = $precision[$count_columns];
        $column_stuff[$count_columns]{'scale'}     = $scale[$count_columns];
        $column_stuff[$count_columns]{'maxwidth'}  = length($this_col_name); # will may be updated below

        my @right_justify_types = ( 'DECIMAL' , 'DATE' , 'NUMBER' , 'DOUBLE PRECISION' );
        if ( grep m/$type_names[$count_columns]/ , @right_justify_types ) {
            $column_stuff[$count_columns]{'justify'} = 'right';
        } else {
            $column_stuff[$count_columns]{'justify'} = 'left';
        }
        $count_columns++;
    }

    return \@column_stuff;

}


##########################################################################
# sub gen_table_header
#   input: list of hashes with column information
#   return: table header string
#   Generates table header to use when displaying data retrieved
##########################################################################
sub gen_table_header {

    my @column_stuff = @_;
    my $header = '';
    my $count_columns = 0;
    my @separators;
    $header .= "\n";
    foreach my $this_column ( @column_stuff ) {

        my $width = $column_stuff[$count_columns]{'maxwidth'};
        for ( my $i = 1 ; $i <= $width ; $i++ ) { $separators[$count_columns] .= '-'; }
        $header .= sprintf( "%-${width}s", ${$this_column}{'col_name'});
        $header .= ' ';
        $count_columns++;
    }
    $header .= "\n";

    foreach my $this_line ( @separators ) { $header .= "$this_line "; }
    $header .= "\n";

    return $header;
}


##########################################################################
# sub run_sql_command
#   input: database handle, sql command
#   return: list of references to lists containing table and header data
#   Passes whatever it's been given on to the database returns the result
##########################################################################
sub run_sql_command {

    my ( $dbh_ref , $sql_cmd ) = @_;

    my $sth_ref = $dbh_ref->prepare($sql_cmd) or print "\nSQL fail!\n\n";
    if ( $sth_ref ) {
        my $ary_ref = $dbh->selectall_arrayref($sql_cmd);
        my $ary_info_ref = &get_column_data ( $dbh_ref , $sth_ref );
        return ( $ary_ref , $ary_info_ref );
    } else {
        return ( \"This is not an array reference" , \"Neither is this" );
    }
    

}

##########################################################################
# sub format_result
#   input: reference to array containing data returned by database,
#       and reference to array containing column information
#   return: string of formatted data
#   Puts the result of SQL command into a format approximating sqlplus
##########################################################################
sub format_result {

    my ( $out_ref , $out_info_ref ) = @_;
    my $display_result = '';

    my @column_info = @$out_info_ref;

    ## loop over table to determine the widest element in each column
    #
    foreach my $this_row ( @$out_ref ) {
        my $col_count = 0;
        foreach my $this_col ( @$this_row ) {

            ## using (NULL) to represent null values, so length is 6
            #
            my $colwidth = ( defined($this_col) ) ? length( $this_col ) : 6;

            if ( $colwidth > $column_info[$col_count]{'maxwidth'} ) {
                $column_info[$col_count]{'maxwidth'} = $colwidth;
            }
            $col_count++;
        }

    }

    my $header_string = &gen_table_header( @column_info );
    $display_result .= "$header_string";

    my $row_count = 0;
    foreach my $this_row ( @$out_ref ) {
        my $count_columns = 0;
        foreach my $this_column ( @$this_row ) {

            my $col_width = $column_info[$count_columns]{'maxwidth'};
            my $justify = "%-";
            if ( $column_info[$count_columns]{'justify'} eq 'right' ) {
                $justify = "%";
            }

            if ( ! defined $this_column ) { $this_column = '(NULL)'; }
            $display_result .= sprintf( "$justify"."$col_width"."s", $this_column);
            $display_result .=  " ";
            $count_columns++;
        }
        $row_count++;
        $display_result .= "\n";
    }
    $display_result .= "\n$row_count rows selected.\n\n";

    return $display_result;
}


##########################################################################
# sub desc_table
#   input: database handle, table name
#   return: string containing formatted info, and list of column names
#   The list of column names is to be used for tab-completion
##########################################################################
sub desc_table {

    my ( $dbh_ref , $table_name ) = @_;

    my $desc_cmd = uc("
        SELECT  COLUMN_NAME as Name,
                DECODE ( NULLABLE, 'N' , 'NOT NULL' , ' ' ) as NULLABLE,
                DATA_TYPE
                    || DECODE
                        (      ( NVL(CHAR_LENGTH,0) + NVL(DATA_PRECISION,0) ) ,
                        0  ,
                        '' ,
                        '(' || ( NVL(CHAR_LENGTH,0) + NVL(DATA_PRECISION,0) ) 
                            || DECODE
                                ( DATA_SCALE , NULL , '' , 0 , '' , ',' || DATA_SCALE )
                            || ')'
                        )
                    as Type
        FROM    USER_TAB_COLUMNS
        WHERE   TABLE_NAME = '$table_name'
    ");
    if ( $debug ) { print "SQL here:====\n$desc_cmd\n====\n"; }

    my ( $result_ref , $result_columninfo_ref ) = &run_sql_command( $dbh_ref , $desc_cmd );

    ## print warning if no rows returned
    #
    my $row_count = @$result_ref;
    if ( $row_count == 0 ) {
        my $nothing_to_see = "\nNo table $table_name in USER_TAB_COLUMNS\n\n";
        my @nonames = ();
        return ( $nothing_to_see , @nonames );
    } else {
        my $result_out = &format_result( $result_ref , $result_columninfo_ref );
        my @column_names = &get_column_names( $dbh , $table_name );
        return ( $result_out , @column_names );
    }

}


##########################################################################
# sub loadcliopts
#   input: none
#   return: reference to hash of options given on CLI
#   Used to load command line options
##########################################################################
sub loadcliopts {
    my %options;
    GetOptions (
        'debug'         => \$options{'debug'},
        'oracle_user=s' => \$options{'ORACLE_USER'},
        'oracle_pass=s' => \$options{'ORACLE_PASS'},
        'oracle_sid=s'  => \$options{'ORACLE_SID'},
    );
    return \%options;
}
